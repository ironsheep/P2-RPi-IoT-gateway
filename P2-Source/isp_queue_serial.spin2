'' =================================================================================================
''
''   File....... isp_queue_serial.spin2
''   Purpose.... provide serial line receiver that queues each line received
''
''   Authors.... Stephen M. Moraco
''               -- see below for terms of use
''   E-mail..... stephen@ironsheep.biz
''   Started.... Jan 2022
''   Updated.... 20 Jan 2022
''
'' =================================================================================================

' implemented as a receive COG storing characters in a larger circular buffer. As complete strings
' are identified (denoted by LF char at end) the LF is replaced by a zero terminator and the
' pointer to the newly arrived string is stored in a circular queue of arrived strings.

' There are two types of strings being received:  strings prefixed with "ctrl:" which denote
' control value changes arriving at P2 from RPi and all others which are status reports
' or lists of information requested by the P2 of the RPi.

' control value changes are stored in one queue while status reports are stored in another
'  this provides a means to isolate the two streams so status messages can arrive without
'  having to wait for the control messages to be processed.

' another advantage is that a whole group of control value changes can arrive before any of
' them need to be processed.

CON { Object Interface: PUBLIC ENUMs }

' parse (PSE) results
  #0, PSE_UNKNOWN, PSE_SUCCESS, PSE_FAILURE

CON { fixed io pins }

  RX1      = 63  { I }                                          ' programming / debug
  TX1      = 62  { O }

  SF_CS    = 61  { O }                                          ' serial flash
  SF_SCK   = 60  { O }
  SF_SDO   = 59  { O }
  SF_SDI   = 58  { I }

OBJ { Objects Used by this Object }

    RPi         : "jm_serial"                                   ' serial I/O
    nstr        : "jm_nstrings"                                 ' number-to-string

CON { driver config values }

    STACK_SIZE_LONGS = 64   ' 48, 32 crashes!

    RX_CHR_Q_MAX_BYTES = 512    ' 80 = testing size, else 512
    MAX_SINGLE_STRING_LEN = 128 ' 79 = testing size, else 128
    RX_STR_Q_MAX_LONGS = 10  ' 10 strings waiting to be picked up - MAX


CON { test values }
{
  CLK_FREQ = 200_000_000                                        ' system freq as a constant
  _clkfreq = CLK_FREQ                                           ' set system clock

  RX_GW    = 25  '- I -                                          ' Raspberry Pi (RPi) Gateway
  TX_GW    = 24  '- O -

  GW_BAUDRATE = 500_000   ' 500kb/s - allow P2 rx to keep up!
}
  DO_NOT_WRITE_MARK = $addee5e5
  NOT_WRITTEN_MARK = $a5a50df0

VAR { pin info saves }

    long    pinRx
    long    pinTx

    long    rxCogId

DAT { Queued RX data }

' user set variables at task init
pRxByteStart    long    0
nRxByteMax      long    0

bTestMode       long    FALSE

' TASK use variables
taskStack       long    0[STACK_SIZE_LONGS]
endStackMark    long    DO_NOT_WRITE_MARK

pRxByteHead     long    0   ' our QUEUE control vars - received BYTEs
nRxByteCount    long    0

bInString       long    FALSE
pNewStrStart    long    0

pRsltStrStart   long    0
pRsltQueOverrun long    FALSE

bQueOverrun     long    FALSE
bStrQueOverrun  long    FALSE

nQStrHeadIdx    long    0   ' our QUEUE control vars - received strings
nQStrTailIdx    long    0
nQStrCount      long    0
nQStrFill       long    $deadf00d
rxStrPtrSet     long    0[RX_STR_Q_MAX_LONGS]   ' our circular buffer - ptrs to received strings

serialRxBffr    byte    0[RX_CHR_Q_MAX_BYTES]   ' our circular buffer - received chars

' task vars
nLoopCt         long    0
pNextMsg        long    0
nTaskChr        long    0
' tsk vars

strCtrlPrefix   byte    "ctrl:",0

CON { - - - - - }

CON { test control values }

  #0, CT_UNKNOWN, CT_IDLE, CT_STOP, CT_PLACE_STR, CT_PLACE_N_MSGS

  #0, TST_UNKNOWN, TST_PASS, TST_FAIL

DAT { test control vars }

bDbgShoMem      long    FALSE

eTestCtrl       long    0
eTestParam      long    0
eTestCtrlPrior  long    0

ctStrIdle       byte    "IDLE",0
ctStrStop       byte    "STOP",0
ctStrPlcStr     byte    "PLACE_STR",0
ctStrPlcNMsgs   byte    "PLACE_N_MSGS",0
ctStrUnknown    byte    "CT_????",0

greenStr        byte    "Green String", $0a, 0
redStr        byte    "Red String", $0a, 0

userRxBuffer    byte    0[MAX_SINGLE_STRING_LEN+1]

nStackDepth          long    STACK_SIZE_LONGS

PUB null() | pWrappedStr, bStatus, pStr, bWaitStatus, nCtParm, bPassFail
'' This is not a top level object

    if bTestMode == TRUE
    ' but is our test code main for now...
        'startx(RX_GW, TX_GW, GW_BAUDRATE)
        ' =========================================
        ' test code
        ' -----------------------------------------
    {
        ' have serial place str in buffer
        eTestCtrl := CT_IDLE
        waitms(250)

        eTestParam := @greenStr
        eTestCtrl := CT_PLACE_STR
        ' then report the string arrived
        ' check to see if string arrived
        '  display string
        '  free string
        bWaitStatus := TRUE ' yes, let's wait for strings to arrive
        pStr := getRxString(@userRxBuffer, MAX_SINGLE_STRING_LEN, bWaitStatus)

        waitms(25077)
        eTestParam := @redStr
        eTestCtrl := CT_PLACE_STR
        ' then report the string arrived
        ' check to see if string arrived
        '  display string
        '  free string

        pStr := getRxString(@userRxBuffer, MAX_SINGLE_STRING_LEN, bWaitStatus)

        bStatus := haveRxString()
        debug(">> 001 is T/F:", ubin_byte(bStatus))
    }

        ' - drop buffer size to smaller so fewer messages needed for following tests

        resetRxStatus() ' reset problem indicators
        eTestCtrl := CT_IDLE
        waitms(250)

        ' - fill to buffer max detect goood everrun
        nCtParm := 9    ' our N      EXACT FILL of queue space
        eTestParam := nCtParm
        eTestCtrl := CT_PLACE_N_MSGS
        waitms(500)

        if rxStringsCount() < nCtParm
            debug(">> ERR too few strings reported")
        else
            debug(">> all strings reported in")
        'repeat ' hold here

        repeat nCtParm
            pStr := getRxString(@userRxBuffer, MAX_SINGLE_STRING_LEN, bWaitStatus)

        bStatus := haveRxString()
        debug(">> 002 is T/F:", ubin_byte(bStatus))

        ' this test should overrun
        bPassFail :=  (bQueOverrun == TRUE) ? TST_PASS : TST_FAIL
        testReport(string("(1) Rx too many"), bPassFail)

        resetRxStatus() ' reset problem indicators
        flushRxQueue()

        ' - fill to buffer N strings, see if all report correctly
        ' - fill with exactfit messages - ensure wrap works
        nCtParm := 8    ' our N      EXACT FILL of queue space
        eTestParam := nCtParm
        eTestCtrl := CT_PLACE_N_MSGS
        waitms(250)

        repeat while rxStringsCount() < nCtParm
        debug(">> all strings reported in")
        'repeat ' hold here

        repeat nCtParm
            pStr := getRxString(@userRxBuffer, MAX_SINGLE_STRING_LEN, bWaitStatus)

        bStatus := haveRxString()
        debug(">> 003 is T/F:", ubin_byte(bStatus))

        ' this test should NOT overrun
        bPassFail :=  (bQueOverrun == FALSE) ? TST_PASS : TST_FAIL
        testReport(string("(2) Rx Exact full"), bPassFail)

        resetRxStatus() ' reset problem indicators

        ' - fill with non-exact fit messages - ensure wrap works
        bPassFail := TST_PASS
        repeat 4    ' 3 ok
            nCtParm := 3    ' our N      EXACT FILL of queue space
            eTestParam := nCtParm
            eTestCtrl := CT_PLACE_N_MSGS
            waitms(250)

            if rxStringsCount() < nCtParm
                debug(">> ERR too few strings reported")
                bPassFail := TST_FAIL
            else
                debug(">> all strings reported in")

            repeat rxStringsCount()
                pStr := getRxString(@userRxBuffer, MAX_SINGLE_STRING_LEN, bWaitStatus)

        bStatus := haveRxString()
        debug(">> 003 is T/F:", ubin_byte(bStatus))

        ' this test should NOT overrun
        if (bQueOverrun == TRUE)
            bPassFail := TST_FAIL

        testReport(string("(3) Rx wrap test"), bPassFail)

        resetRxStatus() ' reset problem indicators


        ' next tests:
        ' - restore buffer to production size [512b]
        ' - cut over to actual RX with RPi
        ' - do multi-message rx and unload - ensuring this all works
        ' ========= ready for production use =========

        ' =========================================
        stop()

CON { - - - - - }

PUB startx(rxpin, txpin, baud)
'' Start RPi gateway serial coms on rxpin and txpin at baud
    pinRx := rxpin
    pinTx := txpin
    RPi.startx(pinRx, pinTx, baud)                              ' use user serial port

    prepStackForCheck() ' for our internal test use

    ' start our rx task in own cog
    rxCogId := cogspin(newcog, TaskSerialRx(@serialRxBffr, RX_CHR_Q_MAX_BYTES), @taskStack)
    if rxCogId == -1    ' did fail?
        debug("!! ERROR filed to start RX-QUE task")

PUB stop()
'' Release the serial pins and free up the rcvr cog
    RPi.rxflush()   ' git rid of any pending input
    ' free the pins used
    pinf(pinRx)    ' de-assert
    pinf(pinTx)    ' de-assert

    ' free the cog used
    if(rxCogId)
        cogstop(rxCogId - 1)
        rxCogId := 0

CON { ----- Status-Reponses Interface ----- }

  WAIT_FOR_STRING = TRUE

PUB readRPiResponse(pBffr, nBffrMax) : pStr
'' wait for, then return the next status string received
    debug("gStr: entry...")
    pStr := getResultString(pBffr, nBffrMax, WAIT_FOR_STRING)
    debug("gStr: str=[", zstr_(pStr), "]")

PUB haveStatusUpdate() : bResponseStatus
'' return T/F where T means a reponse has been received and needs to be handled
    bResponseStatus := (pRsltStrStart > 0) ? TRUE : FALSE

PUB rxResultStringsCount() : nCount
'' Return count of strings received
    nCount := (pRsltStrStart <> 0) ? 1 : 0

PUB getResultString(pUserDest, lenDest, bShouldWait) : pStr | bStringArrived, pWrappedStr
'' Return {pStr} or 0 if none
'' if {bShouldWait} is TRUE wait until string arrives before returning
    'debug("getRxString: ENTRY")
    pStr := 0
    if bShouldWait == TRUE
        bStringArrived := TRUE
        repeat until haveStatusUpdate() == TRUE
    else
        bStringArrived := haveStatusUpdate()

    if bStringArrived == TRUE
        pStr := pUserDest
        pWrappedStr := dequeueResultRxStr()
        copyWrappedStr(pUserDest, pWrappedStr, lenDest)
        freeWrappedString(pWrappedStr)
        debug("getRsltString: str=[", zstr_(pUserDest), "]")
    else
        debug("getRsltString: str=[] - NOT PRESENT")

CON { ----- Control Changes Interface ----- }

PUB haveControlUpdate() : bControlStatus
'' return T/F where T means a control value change has been received and needs to be handled
    bControlStatus := (rxStringsCount() > 0) ? TRUE : FALSE' none for now...

PUB getControlAndValue(pName, pValue) : nStatus
'' interpret control change request and return KV parts
    nStatus := FALSE

CON { ----- General Strings Interface ----- }

PUB haveRxString() : presentStatus
'' return T/F where T means have received a string!
    presentStatus := (nQStrCount > 0) ? TRUE : FALSE
    'debug("?? haveRxString[T/F]: ", ubin_byte(presentStatus))

PUB rxStringsCount() : nCount
'' Return count of strings received
    nCount := nQStrCount

PUB getRxString(pUserDest, lenDest, bShouldWait) : pStr | bStringArrived, pWrappedStr
'' Return {pStr} or 0 if none
'' if {bShouldWait} is TRUE wait until string arrives before returning
    'debug("getRxString: ENTRY")
    pStr := 0
    if bShouldWait == TRUE
        bStringArrived := TRUE
        repeat until haveRxString() == TRUE
    else
        bStringArrived := haveRxString()

    if bStringArrived == TRUE
        pStr := pUserDest
        pWrappedStr := dequeueRxStr()
        copyWrappedStr(pUserDest, pWrappedStr, lenDest)
        freeWrappedString(pWrappedStr)
        debug("getRxString: str=[", zstr_(pUserDest), "]")
    else
        debug("getRxString: str=[] - NOT PRESENT")

PUB flushRxQueue()
'' reset all Rx Queue control and data (emptying it!)
    bytefill(@serialRxBffr, 0, RX_CHR_Q_MAX_BYTES)
    longfill(@rxStrPtrSet, 0, RX_STR_Q_MAX_LONGS)
    pRxByteHead := pRxByteStart

    bInString := FALSE

    nQStrHeadIdx := 0
    nQStrTailIdx := 0

    nRxByteCount := 0
    nQStrCount := 0

    pRsltStrStart := 0

PUB resetRxStatus()
'' reset all task failure indicators
    bQueOverrun := FALSE
    bStrQueOverrun := FALSE


CON { ----- TASK ----- }
' ====================================================
'  SERIAL RX QUEUE routines
'
PRI TaskSerialRx(pRxBffr, lenRxBffr)
' our serial receive to queue loop
    ' preserve incoming values
    pRxByteStart := pRxBffr
    nRxByteMax := lenRxBffr

    ' NOTE parameters are for DEBUG use only
    flushRxQueue()

    debug("TASK[Rx] started ", uhex(pRxByteStart), ", ", udec(nRxByteMax), ", rxBffr=[", uhex_(@serialRxBffr), "]")

    if bTestMode
        ' run loop (test-version for now)
        debug("TASK[Rx] ** TEST LOOP Running **")
        repeat
            checkStack()
            if (eTestCtrl == CT_PLACE_STR)
                eTestCtrl := CT_IDLE
                tskFakeRxStr(eTestParam)
            elseif (eTestCtrl == CT_PLACE_N_MSGS)
                eTestCtrl := CT_IDLE
                nLoopCt := eTestParam
                repeat nLoopCt
                        pNextMsg := genTestMsg()
                        tskFakeRxStr(pNextMsg)
            if (eTestCtrlPrior <> eTestCtrl)
                showTestState(eTestCtrlPrior, eTestCtrl)
                eTestCtrlPrior := eTestCtrl
    else
        ' read serial input forever placing chars in RX Circ Queue
        debug("TASK[Rx] ** Live LOOP rcvg fm RPi **")
        repeat
            nTaskChr := RPi.rxtime(1000)
            if (nTaskChr <> -1)
                tskEnqueueRx(nTaskChr)

PRI tskFakeRxStr(pStr) | nIdx, nStatusValue
' place string into buffer as if it was received
    debug("TASK[Rx] str=[", zstr_(pStr), "]")
    repeat nIdx from 0 to strsize(pStr) - 1
       nStatusValue := tskEnqueueRx(byte[pStr][nIdx])
       if nStatusValue
            debug("EEE Abort string write EEE")
            quit

PRI tskEnqueueRx(nChr) : bFailedStatus
' place byte into rx queue, if EOL then place term instead and engueue string ptr!
    bFailedStatus := FALSE
    if (nRxByteCount < nRxByteMax)
        if bInString == FALSE
            bInString := TRUE
            pNewStrStart := pRxByteHead
            'debug("TASK[Rx] ", uhex_long(pNewStrStart), ubin_byte(bInString))
        'debug("TASK[Rx] rxChr=", uhex_(nChr))
        nRxByteCount++
        if (nChr <> $0a)
            byte [pRxByteHead++] := nChr
        else
            byte [pRxByteHead++] := $00 ' place term instead of EOL
            if bInString == TRUE
            if strHasPrefix(pNewStrStart, @strCtrlPrefix) == TRUE
                tskEnqueueRxStr(pNewStrStart)
            else
                tskEnqueueResultRxStr(pNewStrStart)
                bInString := FALSE
        ' wrap ptr if needed...
        if pRxByteHead > @BYTE [@serialRxBffr][RX_CHR_Q_MAX_BYTES - 1]
            pRxByteHead := @serialRxBffr
    else
        bQueOverrun := TRUE ' signal that we lost incoming data!!!
        debug("TASK[Rx] !! ERROR  char-queue full!! ", sdec(nRxByteCount), sdec(nRxByteMax))
        bFailedStatus := TRUE

PRI strHasPrefix(pTargetStr, pPrefix) : bHasStatus | nIdx
    bHasStatus := TRUE
    repeat nIdx from 0 to strsize(pPrefix)
        if BYTE[pTargetStr][nIdx] <> BYTE[pPrefix][nIdx]
            bHasStatus := False
            quit    ' outta here, we have our answer!

PRI tskEnqueueResultRxStr(pStr)
' report string arrival to listener (place string pointer in queue)
    if pRsltStrStart <> 0
        pRsltQueOverrun := TRUE
    else
        pRsltStrStart := pStr
        'debug("TASK[Rx] enqueueRsltStr(", udec_(strsize(pStr)), ")=[", zstr_(pStr), "] ", uhex_long(pStr))

PRI tskEnqueueRxStr(pStr)
' report string arrival to listener (place string pointer in queue)
    checkGoodStringPtr(pStr)
    if nQStrCount < RX_STR_Q_MAX_LONGS
        LONG [@rxStrPtrSet][nQStrHeadIdx++] := pStr
        ' if head goes off end-of-set then wrap
        if nQStrHeadIdx > RX_STR_Q_MAX_LONGS - 1
            nQStrHeadIdx := 0
        ' mark arrival of new in queue
        nQStrCount++
        'debug("TASK[Rx] enqueueStr(", udec_(strsize(pStr)), ")=[", zstr_(pStr), "] ", uhex_long(pStr))
    else
        bStrQueOverrun := TRUE ' signal that we lost incoming data!!!
        debug("TASK[Rx] !! ERROR string-queue full!!")
    'debug("TASK[Rx] enqueueStr: ", udec(nQStrCount))

PRI checkGoodStringPtr(pStr)
' report and halt if string not valid (with circular queue range)
if pStr < @serialRxBffr or pStr > @BYTE[@serialRxBffr][RX_CHR_Q_MAX_BYTES-1]
   debug("EEE have BAD ptr to string! ", uhex_long(pStr), uhex_long(pRxByteStart), uhex_long(pRxByteStart + RX_CHR_Q_MAX_BYTES - 1))
   lockupForAnly()

PRI lockupForAnly()
' tell that we are halting then halt (effictivly)
    debug("---- FAIL - stopping here for Analysis of above ^^^")
    repeat      ' hold here for now

CON { ----- Support ----- }

PRI dequeueRxStr() : pRmStr
' remove string from queue, listener done with it
    'dumpStrQ(string("B4 String Ptr Que"))
    if nQStrCount > 0
        pRmStr := LONG[@rxStrPtrSet][nQStrTailIdx++]
        if nQStrTailIdx > RX_STR_Q_MAX_LONGS - 1
            nQStrTailIdx := 0
        nQStrCount--
        'if bDbgShoMem
        debug("-- dequeueRxStr:", uhex_long(pRmStr), udec(nQStrCount))
    else
        debug("-- dequeueRxStr() !! ERROR string-queue empty!!")
    'dumpStrQ(string("FTER String Ptr Que"))

PRI dequeueResultRxStr() : pRsltStr
' remove string from queue, listener done with it
    'dumpStrQ(string("B4 String Ptr Que"))
    pRsltStr := 0
    if pRsltStrStart <> 0
        pRsltStr := pRsltStrStart
        pRsltStrStart := 0  ' mark as empty

PRI freeWrappedString(pRmStr)
    ' zero our string memory
    if bDbgShoMem
        debug("-- freeWrappedString:", uhex_long(pRmStr))
    zeroWrappedStr(pRmStr)

PRI zeroWrappedStr(pRmStr) | nIdx, pSrc, nLen
' fill space occuppied by string with zero's
'  NOTE handle buffer wrap!
'    string can start near and and wrap to front!
    pSrc := pRmStr
    nLen := strsize(pRmStr)
    repeat nIdx from 0 to nLen - 1
        ' if pointing beyond end, wrap to front!
        if pSrc > @BYTE[@serialRxBffr][RX_CHR_Q_MAX_BYTES-1]
          pSrc -= RX_CHR_Q_MAX_BYTES
        BYTE[pSrc++] := 0
    nRxByteCount -= nLen + 1
    if bDbgShoMem
        debug("-- zeroWrappedStr: ", uhex_long(pRmStr), udec(nLen), udec(nRxByteCount))

PRI copyWrappedStr(pUserDest, pSrcStr, lenDest) | nIdx, pSrc, pDest, destLen
' copy possible wrapped string {pSrcStr} to {pUserDest} (use min(strlen,lenDest) as bytes to move)
'  NOTE handle buffer wrap!
'    string can start near and and wrap to front!
    pDest := pUserDest
    destLen := strsize(pSrcStr)
    if (destLen > lenDest)
      destLen := lenDest
    pSrc:= pSrcStr
    repeat nIdx from 0 to destLen - 1
        ' if pointing beyond end, wrap to front!
        if pSrc > @BYTE[@serialRxBffr][RX_CHR_Q_MAX_BYTES-1]
          pSrc -= RX_CHR_Q_MAX_BYTES
        BYTE[pDest++] := BYTE[pSrc++]
    BYTE[pDest] := 0  ' place final terminator
    if bDbgShoMem
        debug("-- copyWrappedStr: str=[", zstr_(pUserDest), "]")

CON { ----- TEST Support ----- }

PRI prepStackForCheck()
' reset all failure indicators

    longfill(@taskStack, NOT_WRITTEN_MARK, STACK_SIZE_LONGS)
    endStackMark := DO_NOT_WRITE_MARK

PRI checkStack()
    if endStackMark <> DO_NOT_WRITE_MARK
        debug("^^^ STACK Overflow! Depth greater than ", udec(nStackDepth),  " longs")
        lockupForAnly()

PRI reportStackUse()
    nStkChkUsed := 0
    repeat nStkChkIdx from 0 to STACK_SIZE_LONGS - 1
        if LONG[@taskStack][nStkChkIdx] <> NOT_WRITTEN_MARK
            nStkChkUsed++
        else
            quit    ' done we have our count
    debug("^^^ STACK used ", udec(nStkChkUsed), " of ", udec(nStackDepth))

PRI testReport(pTestId, bPassFail) | pResult
    pResult := (bPassFail == TST_PASS) ? @rsltPass : @rsltFail
    debug("+++ ---------")
    debug("+++ TEST [", zstr_(pTestId), "] - ", zstr_(pResult))
    reportStackUse()
    checkStack()
    'dumpStack()

    if bPassFail == TST_FAIL
        lockupForAnly()

PRI dumpStack()
    dbgMemDump(@taskStack, (STACK_SIZE_LONGS + 1) * 4, string("TASK Stack"))

DAT { test message things }

testMsgCt       LONG    0

testMsgStr      BYTE    "Test "
testMsgNbr      BYTE    "000x"
testMsgEOL      BYTE    $0a, 0

rsltPass        BYTE    "pass",0
rsltFail        BYTE    "FAIL",0

nStkChkUsed     LONG    0
nStkChkIdx      LONG    0

PRI genTestMsg() : pMsg | nDigit, nRemainder
' return pointer to a simple message
    pMsg := @testMsgStr
    nRemainder := testMsgCt++
    if nRemainder > 999
        nDigit := nRemainder / 1000
        nRemainder -= nDigit * 1000
        BYTE[@testMsgNbr][0] := nDigit + $30
    if nRemainder > 99
        nDigit := nRemainder / 100
        nRemainder -= nDigit * 100
        BYTE[@testMsgNbr][1] := nDigit + $30
    if nRemainder > 9
        nDigit := nRemainder / 10
        nRemainder -= nDigit * 10
        BYTE[@testMsgNbr][2] := nDigit + $30
    BYTE[@testMsgNbr][3] := nRemainder + $30

PRI showTestState(ePrior, eNew) | pFrom, pTo
' display task state
    pFrom := nameForState(ePrior)
    pTo := nameForState(eNew)
    debug("TEST state [", zstr_(pFrom), "] -> [", zstr_(pTo), "]")

PRI nameForState(eNew) : pStr
' return string for state value
    if eNew == CT_IDLE
      pStr := @ctStrIdle
    elseif  eNew == CT_STOP
      pStr := @ctStrStop
    elseif  eNew == CT_PLACE_STR
      pStr := @ctStrPlcStr
    elseif  eNew == CT_PLACE_N_MSGS
      pStr := @ctStrPlcNMsgs
    else
      pStr := @ctStrUnknown

PRI dumpStrQ(pMsg)
' dump our full string-que so we can visually inspect
    dbgMemDump(@nQStrHeadIdx, (4 + RX_STR_Q_MAX_LONGS) * 4, pMsg)

PRI dbgMemDump(pBytes, lenBytes, pMessage) | rowCount, rowLen, pCurrByte, lastRowByteCount, bytesSoFar
' Dump rows of hex values with address preceeding

    if pMessage
        debug("** ", zstr_(pMessage), ":")

    rowCount := lenBytes / 16
    lastRowByteCount := lenBytes - (rowCount * 16)
    pCurrByte := pBytes
    bytesSoFar := 0

    ' emit full lines
    if rowCount > 0
        repeat rowCount
            dbgMemDumpRow(pCurrByte, 16)
            pCurrByte += 16
            bytesSoFar += 16

    if  bytesSoFar < lenBytes
        ' emit last line
        dbgMemDumpRow(pCurrByte, lastRowByteCount)

PRI dbgMemDumpRow(pBytes, lenBytes) '| rowCount, rowLen, pCurrByte, bytIndex
' emit address followed by bytes
    debug(" ", uhex_long_(pBytes), ": ", uhex_byte_array_(pBytes, lenBytes))


CON { license }

{{

  Terms of Use: MIT License

  Permission is hereby granted, free of charge, to any person obtaining a copy of this
  software and associated documentation files (the "Software"), to deal in the Software
  without restriction, including without limitation the rights to use, copy, modify,
  merge, publish, distribute, sublicense, and/or sell copies of the Software, and to
  permit persons to whom the Software is furnished to do so, subject to the following
  conditions:

  The above copyright notice and this permission notice shall be included in all copies
  or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
  INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
  PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
  CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE
  OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

}}
