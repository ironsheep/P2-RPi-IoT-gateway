'' =================================================================================================
''
''   File....... RPi-IoT-gw.spin2
''   Purpose.... Provide IoT access methods
''   Authors.... Stephen M Moraco
''               -- Copyright (c) 2022 Iron Sheep Productions, LLC
''               -- see below for terms of use
''   E-mail..... stephen@ironsheep.biz
''   Started.... Jan 2022
''   Updated.... 11 Jan 2022
''
'' =================================================================================================

CON { Object Interface: PUBLIC ENUMs }

  #0, OP_SUCCESS, OP_FAILURE

' Enum: Folder Id values (EFI_*) - use in fileAccess() calls
  #100, EFI_VAR, EFI_TMP, EFI_CONTROL, EFI_STATUS, EFI_LOG, EFI_MAIL, EFI_PROC
  #200, FM_READONLY, FM_WRITE, FM_WRITE_CREATE

CON { fixed io pins }

  RX1      = 63  { I }                                          ' programming / debug
  TX1      = 62  { O }

  SF_CS    = 61  { O }                                          ' serial flash
  SF_SCK   = 60  { O }
  SF_SDO   = 59  { O }
  SF_SDI   = 58  { I }

OBJ { Objects Used by this Object }

    RPi         : "jm_serial"                                   ' serial I/O
    nstr        : "jm_nstr"                                     ' number-to-string

CON { driver config values}

  RX_BFFR_LEN = 80


VAR { pin info saves }

    long    rx_pin
    long    tx_pin

    byte    rx_bffr[RX_BFFR_LEN+1]

DAT { string constants }

spin2ver    byte    "0.0.1",0

' -------------------------------
' the following are identical to that found in our deamon python script
'   (!!!they must be kept in sync!!!)
parmSep     byte    "^|^",0     ' chars that will not be found in user data
bodyStart   byte    "emailStart",0
bodyEnd     byte    "emailEnd",0
' -------------------------------

PUB null()
'' This is not a top level object

pub startx(rxpin, txpin, baud)
'' Start RPi gateway serial coms on rxpin and txpin at baud
    rx_pin := rxpin
    tx_pin := txpin
    RPi.startx(rx_pin, tx_pin, baud)                              ' use user serial port

pub stop()
'' Release the serial pins (if needed)
    RPi.rxflush()   ' git rid of any pending input
    ' free the pins used
    pinf(rx_pin)    ' de-assert
    pinf(tx_pin)    ' de-assert

pub identify(pHardwareId)
'' Report to the RPi the name of this device (for email signature, etc.)
    RPi.fstr3(string("ident:hwName=%s%sobjVer=%s\n"), pHardwareId, @parmSep, @spin2ver)

pub sendEmail(pEmailTo, pEmailFrom, pEmailSubj, pEmailBody)
'' Tell the RPi to send an email given these specifics
    RPi.fstr5(string("email-send:to=%s%sfm=%s%ssu=%s\n"), pEmailTo, @parmSep, pEmailFrom, @parmSep, pEmailSubj)
    RPi.fstr3(string("%s\n%s\n%s\n"), @bodyStart, pEmailBody, @bodyEnd)
    ' TODO add expectation of return of success or failure

pub sendSMS(pSmsPhoneNbr, pSmsTextMessage)
'' Tell the RPi to send an SMS text message given these specifics
    RPi.fstr3(string("sms-send:phone=%s%smessage=%s\n"), pSmsPhoneNbr, @parmSep, pSmsTextMessage)
    ' TODO add expectation of return of success or failure

pub fileAccess(folderId, fileMode, pFileName) : opStatus, fileId | pStr
'' Tell the RPi to to write named value to file {fileId}
    RPi.fstr5(string("file-access:dir=%d%smode=%d%sfname=%s\n"), folderId, @parmSep, fileMode, @parmSep, pFileName)
    pStr := getString(@rx_bffr, RX_BFFR_LEN)
    ' parse result for success and fileID and return both
    '
    ' Examples: "faccess:status=True,fileid=9999,exists=False\n"
    ' Examples: "faccess:status=True,fileid=9999,exists=True\n"
    ' Examples: "faccess:status=False,msg=error dir=999\n"
    ' Examples: "faccess:status=False,msg=error failed-to-create\n"

pub fileWrite(pFileId, pVarName, pVarValue) : opStatus | pStr
'' Tell the RPi to to write named value to file {fileId}
    RPi.fstr5(string("file-write:fid=%s%skey=%s%sval=%s\n"), pFileId, @parmSep, pVarName, @parmSep, pVarValue)
    pStr := getString(@rx_bffr, RX_BFFR_LEN)

pub fileRead(pFileId, pVarName) : opStatus, varValue | pStr
    RPi.fstr3(string("file-read:fid=%s%skey=%s\n"), pFileId, @parmSep, pVarName)
    pStr := getString(@rx_bffr, RX_BFFR_LEN)

PRI getString(pBffr, nBffrMax) : pStr | nRxLenRem, pRxNextChar, nChr
' read serial input until newline received
    pStr := pBffr
    nRxLenRem := nBffrMax
    pRxNextChar := pBffr
    nChr := 0
    repeat
        if RPi.rxcheck()
            nChr := RPi.rx()
            if nChr == $0a
                byte [pRxNextChar] := $00 ' place term. char
            else
                byte [pRxNextChar] := nChr
            pRxNextChar++
            nRxLenRem--
    until nChr == $0a or nRxLenRem == 0


CON { license }
{{

 -------------------------------------------------------------------------------------------------
  MIT License

  Copyright (c) 2022 Iron Sheep Productions, LLC

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
 =================================================================================================
}}
